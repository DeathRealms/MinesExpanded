plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
}

group = 'me.deathrealms'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "enginehub"
        url = "https://maven.enginehub.org/repo/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
    compileOnly 'com.sk89q.worldedit:worldedit-core:7.2.0-SNAPSHOT'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.2.0-SNAPSHOT'
    implementation 'org.incendo:cloud-paper:2.0.0-beta.10'
    implementation 'org.incendo:cloud-minecraft-extras:2.0.0-beta.10'
    implementation 'org.incendo:cloud-annotations:2.0.0'
    implementation "dev.triumphteam:triumph-gui:3.1.11"
    implementation "net.kyori:adventure-api:4.18.0"
    implementation "net.kyori:adventure-text-minimessage:4.18.0"
    implementation "net.kyori:adventure-platform-bukkit:4.3.4"
    implementation 'org.json:json:20240303'
    implementation 'de.rapha149.signgui:signgui:2.5.0'

}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate 'org.incendo.cloud', 'me.deathrealms.minesexpanded.libs.cloud'
    relocate 'dev.triumphteam.gui', 'me.deathrealms.minesexpanded.libs.gui'
    relocate 'net.kyori', 'me.deathrealms.minesexpanded.libs.kyori'
    relocate 'de.rapha149.signgui', 'me.deathrealms.minesexpanded.libs.signgui'
    minimize() {
        exclude(dependency("de\\.rapha149\\.signgui:signgui:.*"))
    }
    destinationDirectory.set(file("server/plugins"))

    def commitHash = System.getenv("COMMIT_HASH") ?: "unknown"
    if (commitHash == "unknown") {
        archiveFileName.set("MinesExpanded-${version}.jar")
    } else {
        archiveFileName.set("MinesExpanded-${version}-${commitHash}.jar")
    }
}

build.dependsOn shadowJar
